pragma Singleton

import QtQuick
import Quickshell
import Quickshell.Services.UPower

Singleton {
    id: root

    readonly property var powerProfiles: PowerProfiles
    readonly property bool available: powerProfiles && powerProfiles.hasPerformanceProfile
    property int profile: powerProfiles ? powerProfiles.profile : PowerProfile.Balanced

    // Not a power profile but a volatile property to quickly disable shadows, animations, etc..
    property bool noctaliaPerformanceMode: false

    function getName(p) {
        if (!available)
            return "Unknown";

        const prof = (p !== undefined) ? p : profile;

        switch (prof) {
        case PowerProfile.Performance:
            return "Performance";
        case PowerProfile.Balanced:
            return "Balanced";
        case PowerProfile.PowerSaver:
            return "Power saver";
        default:
            return "Unknown";
        }
    }

    function getIcon(p) {
        if (!available)
            return "balanced";

        const prof = (p !== undefined) ? p : profile;

        switch (prof) {
        case PowerProfile.Performance:
            return "rocket_launch";
        case PowerProfile.Balanced:
            return "balance";
        case PowerProfile.PowerSaver:
            return "energy_savings_leaf";
        default:
            return "balanced";
        }
    }

    function init() {
    // Logger.d("PowerProfileService", "Service started");
    }

    function setProfile(p) {
        if (!available)
            return;
        try {
            powerProfiles.profile = p;
        } catch (e) {
            // Logger.e("PowerProfileService", "Failed to set profile:", e);
        }
    }

    function cycleProfile() {
        if (!available)
            return;
        const current = powerProfiles.profile;
        if (current === PowerProfile.Performance)
            setProfile(PowerProfile.PowerSaver);
        else if (current === PowerProfile.Balanced)
            setProfile(PowerProfile.Performance);
        else if (current === PowerProfile.PowerSaver)
            setProfile(PowerProfile.Balanced);
    }

    function cycleProfileReverse() {
        if (!available)
            return;
        const current = powerProfiles.profile;
        if (current === PowerProfile.Performance)
            setProfile(PowerProfile.Balanced);
        else if (current === PowerProfile.Balanced)
            setProfile(PowerProfile.PowerSaver);
        else if (current === PowerProfile.PowerSaver)
            setProfile(PowerProfile.Performance);
    }

    function isDefault() {
        if (!available)
            return true;
        return (profile === PowerProfile.Balanced);
    }

    Connections {
        target: powerProfiles
        function onProfileChanged() {
            root.profile = powerProfiles.profile;
            // Only show toast if we have a valid profile name (not "Unknown")
            const profileName = root.getName();
            if (profileName !== "Unknown") {
                // ToastService.showNotice("toast.power-profile.profile-name", {
                //    "profile": profileName
                // }), "toast.power-profile.changed", profileName.toLowerCase().replace(" ", "");
            }
        }
    }

    // Noctalia Performance Mode
    // - Turning shadow off
    // - Turning animation off
    // - Do Not Disturb
    function toggleNoctaliaPerformance() {
        noctaliaPerformanceMode = !noctaliaPerformanceMode;
    }

    function setNoctaliaPerformance(value) {
        noctaliaPerformanceMode = value;
    }

    onNoctaliaPerformanceModeChanged: {
        if (noctaliaPerformanceMode) {
            // ToastService.showNotice("toast.noctalia-performance.label", "toast.noctalia-performance.enabled", "rocket");
        } else {
            // ToastService.showNotice("toast.noctalia-performance.label", "toast.noctalia-performance.disabled", "rocket-off");
        }
    }
}
